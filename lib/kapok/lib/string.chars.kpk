(defprotocol string.chars
  """The string.chars protocol is responsible for
  converting a structure to a binary (only if applicable).
  The only function required to be implemented is
  `to-string` which does the conversion."""

  (to-string [thing]))

(defimpl string.chars atom
  (require atom)

  (defn to-string [:nil]
    ""  ;; doc
    "")

  (defn to-string [atom]
    (atom.to-string atom))
  )

(defimpl string.chars bitstring
  (require erlang)

  (defn to-string [thing] (&when (binary? thing))
    thing)

  (defn to-string [_thing]
    ;; TODO revise to use exception
    (throw "cannot convert a bitstring to a string"))
  )

(defimpl string.chars list
  (require list)

  (defn to-string [char-list]
    (list.to-string char-list))
  )

(defimpl string.chars integer
  (require integer)

  (defn to-string [thing]
    (integer.to-string thing))
  )

(defimpl string.chars float
  (require erlang
           io_lib_format)

  (defn to-string [thing]
    (erlang.iolist_to_binary (io_lib_format.fwrite_g thing)))
  )
